buildscript {
    ext {
        kotlinVersion = '1.3.61'
        springBootVersion = '2.2.3.RELEASE'
        junitVersion = '5.3.1'
        jacksonVersion = '2.10.1'

        lombok = 'org.projectlombok:lombok:1.18.8'
    }
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
        classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
        classpath("com.github.ben-manes:gradle-versions-plugin:0.27.0")
    }
}

plugins {
    id 'org.owasp.dependencycheck' version '5.2.4'
}

apply plugin: 'kotlin'
apply plugin: 'kotlin-spring'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.owasp.dependencycheck'
apply plugin: 'idea'
apply plugin: 'com.github.ben-manes.versions'

group 'com.boclips.users'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

idea {
    module {
        testSourceDirs += file('src/clientTest/kotlin')
        testResourceDirs += file('src/clientTest/resources')
    }
}

sourceSets {
    client {
        java {
            srcDir 'src/client/java'
        }
    }

    clientTest {
        kotlin {
            compileClasspath += client.output + main.output + test.output
            runtimeClasspath += client.output + main.output + test.output

            srcDir 'src/clientTest/kotlin'
        }
        resources.srcDir file('src/clientTest/resources')
    }
}

test {
    useJUnitPlatform()
}

compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "1.8"
    }
}

compileClientTestKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "1.8"
    }
}

task testClient(type: Test) {
    testClassesDirs = sourceSets.clientTest.output.classesDirs
    classpath = sourceSets.clientTest.runtimeClasspath

    useJUnitPlatform()
}

test.finalizedBy(testClient)

task buildClient(type: Jar) {
    from(sourceSets.client.runtimeClasspath) {
        include '**/*.class'
        include '**/*.jar'
    }

    archiveName("user-service-client-${version}.jar")
}

task sourcesJarClient(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.client.allSource
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencyManagement {
    imports {
        mavenBom "com.fasterxml.jackson:jackson-bom:${jacksonVersion}"
    }
}

dependencies {
    implementation('org.jetbrains.kotlin:kotlin-stdlib-jdk8')
    implementation('org.jetbrains.kotlin:kotlin-reflect')

    implementation('org.springframework.boot:spring-boot-starter-web')
    implementation('org.springframework.boot:spring-boot-starter-hateoas')
    implementation('org.springframework.boot:spring-boot-starter-data-mongodb')
    implementation('org.springframework.boot:spring-boot-starter-security')
    implementation('org.springframework.boot:spring-boot-starter-actuator')
    implementation('org.springframework.retry:spring-retry')
    implementation('org.springframework.boot:spring-boot-starter-aop')

    implementation('com.github.boclips:event-bus:2.11.2')
    implementation('com.github.boclips:boclips-spring-web:0.128.0')
    implementation('com.github.boclips:boclips-spring-security:0.296.0')
    implementation('com.github.boclips:videos:0.839.0')

    implementation('com.jayway.jsonpath:json-path:2.3.0')
    implementation('io.github.microutils:kotlin-logging:1.6.22')
    implementation('com.fasterxml.jackson.module:jackson-module-kotlin:2.10.1')
    implementation('com.fasterxml.jackson.core:jackson-databind')
    implementation('org.keycloak:keycloak-admin-client:4.8.3.Final')
    implementation('org.jboss.resteasy:resteasy-jackson2-provider:3.6.2.Final')
    implementation('org.jboss.resteasy:resteasy-client:3.6.2.Final')
    implementation('org.keycloak:keycloak-authz-client:4.8.3.Final')
    implementation('com.mixpanel:mixpanel-java:1.4.4')
    implementation('com.google.guava:guava:27.0.1-jre')
    implementation('commons-validator:commons-validator:1.6')
    implementation('commons-beanutils:commons-beanutils:1.9.4')
    implementation('io.sentry:sentry-logback:1.7.21')
    implementation('io.micrometer:micrometer-core')
    implementation('io.micrometer:micrometer-registry-prometheus')
    implementation("io.opentracing.contrib:opentracing-spring-jaeger-web-starter:2.0.3")

    clientCompileOnly(lombok)
    clientAnnotationProcessor(lombok)

    clientCompile("org.springframework.security.oauth:spring-security-oauth2:2.4.0.RELEASE")
    clientCompile group: 'org.springframework.security', name: 'spring-security-core', version: '5.2.1.RELEASE'
    clientCompile('com.fasterxml.jackson.core:jackson-databind')
    clientCompile('org.springframework.boot:spring-boot-autoconfigure')

    testImplementation('org.springframework.boot:spring-boot-starter-test')
    testImplementation('org.springframework.security:spring-security-test')
    testImplementation("org.junit.jupiter:junit-jupiter-api:$junitVersion")
    testImplementation("org.junit.jupiter:junit-jupiter-engine:$junitVersion")
    testImplementation("org.junit.jupiter:junit-jupiter-params:$junitVersion")
    testImplementation('org.assertj:assertj-core:3.11.1')
    testImplementation('org.yaml:snakeyaml:1.23')
    testImplementation('de.flapdoodle.embed:de.flapdoodle.embed.mongo')
    testImplementation('com.nhaarman.mockitokotlin2:mockito-kotlin:2.0.0')
    testImplementation('org.awaitility:awaitility:3.1.0')
    testImplementation('org.springframework.cloud:spring-cloud-contract-wiremock:2.1.0.RELEASE')

    clientTestImplementation configurations.testImplementation
    clientTestImplementation configurations.clientImplementation
}

dependencyCheck {
    failBuildOnCVSS = 7 // Fail on high severity or greater

    data {
        directory = './dependency-check-cache'
    }

    analyzers {
        assemblyEnabled = false
    }

    suppressionFile = './dependency-check-suppressions.xml'

    outputDirectory = 'security-report'
}
